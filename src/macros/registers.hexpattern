#include "./heaps.hexpattern"
#include "../types/hexcasting.hexpattern"

#define Read Register (EAST aqqqqqeadeqa) = num -> num
// index -> value
{
    Gemini Decomposition
    Augur's Purification // register 0 is hardwired to 0

    Muninn's Reflection
    Rotation Gambit
    Selection Distillation

    Numerical Reflection: 0
    Augur's Exaltation
}

#define Write Register (EAST deeeeeqadeqa) = num, num ->
// index, value ->
{
    Prospector's Gambit
    Augur's Purification // register 0 is hardwired to 0
    Rotation Gambit II

    Muninn's Reflection
    Rotation Gambit II
    Surgeon's Exaltation

    Muninn's Reflection
    Augur's Exaltation
    Huginn's Gambit
}

#define Read CSR (EAST aqqqqqedaqed) = num -> num
// address -> value
{
    Locate CSR
    Ritualist's Distillation
}

#define Write CSR (EAST deeeeeqdaqed) = num, num ->
// address, value ->
{
    Jester's Gambit
    Locate CSR
    Rotation Gambit
    Ritualist's Gambit II
}

#define Locate CSR (EAST dedqdeqdaqed) = num -> vec, num
// address -> focus position, list index
{
    // focus offset
    Numerical Reflection: 512
    Dioscuri Gambit
    Division Distillation
    Floor Purification
    Vector Reflection +X
    Multiplicative Distillation
    
    // focus position
    Locate Sentinel
    {
        <CSR 0 offset vector>
    }
    Flock's Disintegration
    Additive Distillation
    Additive Distillation

    // index in focus
    Rotation Gambit II
    Modulus Distillation
}
