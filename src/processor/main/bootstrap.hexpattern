#include "../../types/hexcasting.hexpattern"

// pos, startup, shutdown, eval -> startup, shutdown, eval
{
    // inject data positions into eval
    Huginn's Gambit
    {
        Bookkeeper's Gambit: vv-
        Flock's Disintegration

        // injection index
        Muninn's Reflection
        Jester's Gambit
        Locator's Distillation
        Rotation Gambit II

        // y offset + processor pos
        Vector Reflection +Y
        Multiplicative Distillation
        Additive Distillation

        // insert data
        Muninn's Reflection
        Rotation Gambit II
        Surgeon's Exaltation
        Huginn's Gambit
    }
    {
        // [y offset, injection target]
        <[5, Bookkeeper's Gambit: ----]> // trap handlers
        <[4, Bookkeeper's Gambit: --]> // decoders
        <[2, Bookkeeper's Gambit: ---]> // instruction handlers
    }
    Thoth's Gambit
    Bookkeeper's Gambit: v
    
    // inject processor pos into startup and shutdown
    Rotation Gambit
    Undertaker's Gambit
    
    Numerical Reflection: 1
    Jester's Gambit
    Surgeon's Exaltation
    
    Rotation Gambit II
    Numerical Reflection: 1
    Jester's Gambit
    Surgeon's Exaltation

    Jester's Gambit
    Muninn's Reflection // eval
}
