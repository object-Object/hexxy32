#include "../../macros/bytes.hexpattern"
#include "../../macros/registers.hexpattern"

// register-immediate

#define Instruction 0: LUI = num, num ->
// U-type (rd, imm ->)
{
    Write Register
}

#define Instruction 1: AUIPC = num, num ->
// U-type (rd, imm ->)
{
    Numerical Reflection: 32  // PC register
    Read Register
    Additive Distillation
    Write Register
}

#define Instruction 18: ADDI = num, num, num ->
// I-type (rd, rs1, imm ->)
{
    Additive Distillation
    Numerical Reflection: 32
    Truncate Integer
    Write Register
}

#define Instruction 19: SLTI = num, num, num ->
// I-type (rd, rs1, imm ->)
{
    Swap Signedness
    Jester's Gambit
    Swap Signedness
    Jester's Gambit

    Minimus Distillation
    Numerical Reflection: 1
    Numerical Reflection: 0
    Augur's Exaltation

    Write Register
}

#define Instruction 20: SLTIU = num, num, num ->
// I-type (rd, rs1, imm ->)
{
    Minimus Distillation
    Numerical Reflection: 1
    Numerical Reflection: 0
    Augur's Exaltation

    Write Register
}

#define Instruction 21: XORI = num, num, num ->
// I-type (rd, rs1, imm ->)
{
    Exclusionary Distillation
    Write Register
}

#define Instruction 22: ORI = num, num, num ->
// I-type (rd, rs1, imm ->)
{
    Unifying Distillation
    Write Register
}

#define Instruction 23: ANDI = num, num, num ->
// I-type (rd, rs1, imm ->)
{
    Intersection Distillation
    Write Register
}

#define Instruction 24: SLLI = num, num, num ->
// I-type (rd, rs1, imm ->)
{
    Logical Shift Left
    Write Register
}

#define Instruction 25: SRLI = num, num, num ->
// I-type (rd, rs1, imm ->)
{
    Logical Shift Right
    Write Register
}

#define Instruction 26: SRAI = num, num, num ->
// I-type (rd, rs1, imm ->)
{
    Numerical Reflection: 5
    Truncate Integer
    Undertaker's Gambit
    Logical Shift Right

    Numerical Reflection: 32
    Rotation Gambit
    Subtractive Distillation
    Extend Sign

    Write Register
}

// register-register

#define Instruction 27: ADD = num, num, num ->
// R-type (rd, rs1, rs2 ->)
{
    Instruction 18: ADDI
}

#define Instruction 28: SUB = num, num, num ->
// R-type (rd, rs1, rs2 ->)
{
    Two's Complement Purification
    Instruction 18: ADDI
}

#define Instruction 29: SLL = num, num, num ->
// R-type (rd, rs1, rs2 ->)
{
    Numerical Reflection: 5
    Truncate Integer
    Instruction 24: SLLI
}

#define Instruction 30: SLT = num, num, num ->
// R-type (rd, rs1, rs2 ->)
{
    Instruction 19: SLTI
}

#define Instruction 31: SLTU = num, num, num ->
// R-type (rd, rs1, rs2 ->)
{
    Instruction 20: SLTIU
}

#define Instruction 32: XOR = num, num, num ->
// R-type (rd, rs1, rs2 ->)
{
    Instruction 21: XORI
}

#define Instruction 33: SRL = num, num, num ->
// R-type (rd, rs1, rs2 ->)
{
    Numerical Reflection: 5
    Truncate Integer
    Instruction 25: SRLI
}

#define Instruction 34: SRA = num, num, num ->
// R-type (rd, rs1, rs2 ->)
{
    Instruction 26: SRAI
}

#define Instruction 35: OR = num, num, num ->
// R-type (rd, rs1, rs2 ->)
{
    Instruction 22: ORI
}

#define Instruction 36: AND = num, num, num ->
// R-type (rd, rs1, rs2 ->)
{
    Instruction 23: ANDI
}
