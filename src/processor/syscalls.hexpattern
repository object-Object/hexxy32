#include "../types/hexcasting.hexpattern"
#include "../macros/memory.hexpattern"
#include "../macros/registers.hexpattern"

#define Syscall 0: Halt = num, num, num, num, num, num, num ->
// exit code, ... ->
{
    Bookkeeper's Gambit: vvvvvv

    {
        <"Halt syscall executed. The processor will now terminate.">
        <"Exit code:">
    }
    Jester's Gambit
    Integration Distillation
    
    {
        Reveal
    }
    Jester's Gambit
    Thoth's Gambit
    Bookkeeper's Gambit: v

    // set exit flag to stop execution
    Numerical Reflection: 35
    Numerical Reflection: 1
    Write Register
}

#define Syscall 1: Print String = num, num, num, num, num, num, num ->
// address, length, ... ->
// TODO: this could be *way* more efficient (ie. read a page of memory and reuse it until we need the next one)
{
    Bookkeeper's Gambit: vvvvv
    Blank Reflection

    {
        // break if we've printed the entire string
        Rotation Gambit
        Dioscuri Gambit
        Augur's Purification
        Vacant Reflection
        Rotation Gambit
        Augur's Exaltation
        Hermes' Gambit
        
        // length -= 1
        Numerical Reflection: 1
        Subtractive Distillation
        Rotation Gambit II
        
        // address += 1
        Numerical Reflection: 3
        Fisherman's Gambit
        Gemini Decomposition
        Numerical Reflection: 1
        Additive Distillation
        Numerical Reflection: -4
        Fisherman's Gambit
        
        // load and append the current character to the string
        Rotation Gambit
        {
            // ASCII lookup table
            // copy the next line, split on ¦, replace index 10 with a newline using Breaking Reflection
            // ¦¦¦¦¦¦¦¦¦ ¦\n¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦ ¦!¦"¦#¦$¦%¦&¦'¦(¦)¦*¦+¦,¦-¦.¦/¦0¦1¦2¦3¦4¦5¦6¦7¦8¦9¦:¦;¦<¦=¦>¦?¦@¦A¦B¦C¦D¦E¦F¦G¦H¦I¦J¦K¦L¦M¦N¦O¦P¦Q¦R¦S¦T¦U¦V¦W¦X¦Y¦Z¦[¦\¦]¦^¦_¦`¦a¦b¦c¦d¦e¦f¦g¦h¦i¦j¦k¦l¦m¦n¦o¦p¦q¦r¦s¦t¦u¦v¦w¦x¦y¦z¦{¦|¦}¦~¦
            // alternatively, use this one to replace all non-printing characters with ?
            // ?¦?¦?¦?¦?¦?¦?¦?¦?¦ ¦\n¦?¦?¦?¦?¦?¦?¦?¦?¦?¦?¦?¦?¦?¦?¦?¦?¦?¦?¦?¦?¦?¦ ¦!¦"¦#¦$¦%¦&¦'¦(¦)¦*¦+¦,¦-¦.¦/¦0¦1¦2¦3¦4¦5¦6¦7¦8¦9¦:¦;¦<¦=¦>¦?¦@¦A¦B¦C¦D¦E¦F¦G¦H¦I¦J¦K¦L¦M¦N¦O¦P¦Q¦R¦S¦T¦U¦V¦W¦X¦Y¦Z¦[¦\¦]¦^¦_¦`¦a¦b¦c¦d¦e¦f¦g¦h¦i¦j¦k¦l¦m¦n¦o¦p¦q¦r¦s¦t¦u¦v¦w¦x¦y¦z¦{¦|¦}¦~¦?
            <["", ..., " ", "\n", ..., " ", "!", ...]>
        }
        Flock's Disintegration

        // if the byte is outside of the ascii range, print "?" instead
        Rotation Gambit
        Load Byte

        Gemini Decomposition
        Numerical Reflection: 128
        Minimus Distillation
        Jester's Gambit
        Numerical Reflection: 63 // ?
        Augur's Exaltation

        Selection Distillation
        Additive Distillation
        
        // recurse
        Jester's Gambit
        Numerical Reflection: 4
        Fisherman's Gambit II
        Hermes' Gambit
    }
    Numerical Reflection: -3
    Fisherman's Gambit II
    Iris' Gambit

    Bookkeeper's Gambit: vv-vv
    Reveal
    Bookkeeper's Gambit: v
}

#define Syscall 2: Refresh Display = num, num, num, num, num, num, num ->
// screen buffer address, ... ->
{
    Bookkeeper's Gambit: vvvvvv

    // get screen top left position
    Locate Sentinel
    {
        <screen offset vector>
    }
    Flock's Disintegration
    Additive Distillation
    
    // read screen buffer
    // we assume this address is at the start of a memory page for simplicity
    Jester's Gambit
    Load Page
    Numerical Reflection: 0
    Numerical Reflection: 73 // ceil(17*17 / 4 bytes per value)
    Selection Exaltation

    // initial index
    Numerical Reflection: 0
    
    Consideration: {
        // get word
        Dioscuri Gambit
        Undertaker's Gambit
        Numerical Reflection: 4
        Division Distillation
        Floor Purification
        Selection Distillation
        
        // get color value from word (see Load Byte)
        // 2^((3 - (address % 4)) * 8)
        Numerical Reflection: 2

        Rotation Gambit
        Numerical Reflection: 4
        Modulus Distillation

        Numerical Reflection: 3
        Jester's Gambit
        Subtractive Distillation

        Numerical Reflection: 8
        Multiplicative Distillation

        Power Distillation
        Division Distillation
        Floor Purification

        Numerical Reflection: 256 // 2^8
        Modulus Distillation

        Numerical Reflection: 9 // number of available colors
        Modulus Distillation

        // get color iota
        {
            <Garbage>
            <Null>
            // ... (see color table in readme)
        }
        Jester's Gambit
        Selection Distillation
        
        // x offset
        Prospector's Gambit

        Gemini Decomposition
        Numerical Reflection: 17
        Modulus Distillation
        Vector Reflection -X // screen +x direction
        Multiplicative Distillation
        
        // y offset
        Jester's Gambit
        Numerical Reflection: 17
        Division Distillation
        Floor Purification
        Vector Reflection -Y // screen +y direction
        Multiplicative Distillation
        
        // write focal port
        Additive Distillation
        Numerical Reflection: 4
        Fisherman's Gambit II
        Additive Distillation
        
        Jester's Gambit
        Ritualist's Gambit

        // increment index
        Numerical Reflection: 1
        Additive Distillation

        // recurse if still in bounds
        Gemini Decomposition
        Numerical Reflection: 289 // 17*17
        Maximus Distillation II
        Vacant Reflection
        Numerical Reflection: 5
        Fisherman's Gambit II
        Augur's Exaltation
        Hermes' Gambit
    Consideration: }
    Numerical Reflection: -3
    Fisherman's Gambit II
    Hermes' Gambit

    Bookkeeper's Gambit: vvvv
}
