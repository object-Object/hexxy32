#include "./macros/memory.hexpattern"
#include "./macros/heaps.hexpattern"


{
    {
        // load registers
        Gemini Decomposition
        Compass' Purification
        Vector Reflection -Y
        Additive Distillation

        Entity Purification
        Gemini Decomposition
        Chronicler's Purification

        // get program counter, and increment for next cycle
        Gemini Decomposition
        Numerical Reflection: 32
        Selection Distillation

        Undertaker's Gambit
        Numerical Reflection: 1
        Additive Distillation

        Numerical Reflection: 32
        Surgeon's Exaltation
        Huginn's Gambit

        // get instruction index
        Load Word

        Gemini Decomposition
        Numerical Reflection: 128 // 2^7
        Modulus Distillation

        {
            Bookkeeper's Gambit: -- // mapping from opcode to instruction heap index
        }
        Flock's Disintegration
        Locator's Distillation
        Numerical Reflection: 1
        Additive Distillation

        // execute instruction
        Numerical Reflection: 22

        Identity Reflection
        Compass' Purification
        Vector Reflection +Y
        Additive Distillation

        Encyclopedist's Exaltation
        Hermes' Gambit

        // save registers
        Muninn's Reflection
        Chronicler's Gambit
    }

    // target pedestal
    Mind's Reflection
    Compass' Purification
    Mind's Reflection
    Alidade's Purification
    Archer's Distillation

    // save initial state of registers
    Gemini Decomposition
    Entity Purification

    Numerical Reflection: 0
    Numerical Reflection: 33
    Gemini Gambit
    Numerical Reflection: 33
    Flock's Gambit

    Chronicler's Gambit

    // summon processor
    Vector Reflection +Y
    Additive Distillation
    Numerical Reflection: 100
    Summon Cyclic Wisp
}
