#include "../../types/hexcasting.hexpattern"
#include "../../macros/heaps.hexpattern"

{
    Consideration: {
        {
            // drop dummy iota
            Bookkeeper's Gambit: v

            /* rx packet formats:
            get last written index: [0]
            write data: [data, index, 1]
            read data: [index, 2]
            */
            Recitation Reflection
            Flock's Disintegration

            {
                // get last written index
                Bookkeeper's Gambit: v
                Numerical Reflection: 0
                Muninn's Reflection
                Send Iota
            }
            {
                // write data
                // get focus position
                Gemini Decomposition
                Numerical Reflection: 19
                Cubic Index Distillation
                Numerical Reflection: 19
                Locate Heap
                Additive Distillation
                
                // write data
                Rotation Gambit
                Ritualist's Gambit

                // save written index to send later
                Huginn's Gambit
            }
            {
                // read data
                // get focus position
                Gemini Decomposition
                Numerical Reflection: 19
                Cubic Index Distillation
                Numerical Reflection: 19
                Locate Heap
                Additive Distillation
                
                // read data and send to computer
                Ritualist's Purification
                Numerical Reflection: 2
                Flock's Gambit
                Numerical Reflection: 0
                Jester's Gambit
                Send Iota
            }
            Numerical Reflection: 3
            Flock's Gambit
            Jester's Gambit
            Selection Distillation
            Hermes' Gambit
        }

        // if we received iotas, iterate once for each iota in the queue
        // else send null to tell the computer we just connected
        Postmaster's Reflection
        Gemini Decomposition
        Augur's Purification
        {
            Gemini Decomposition
            Gemini Gambit
            Gemini Decomposition
            Flock's Gambit

            Thoth's Gambit
            Bookkeeper's Gambit: v
        }
        {
            Bookkeeper's Gambit: vv
            Numerical Reflection: 0
            Nullary Reflection
            Send Iota
        }
        Augur's Exaltation
        Hermes' Gambit

        Listen
    Consideration: }

    Mind's Reflection
    Compass' Purification
    Mind's Reflection
    Alidade's Purification
    Archer's Distillation

    Undertaker's Gambit
    Undertaker's Gambit

    Numerical Reflection: 100
    Summon Cyclic Wisp

    Entity Purification: Wisp
    Link Others
}
