#include "../../types/hexcasting.hexpattern"
#include "../../macros/heaps.hexpattern"

{
    Consideration: {
        {
            // drop dummy iota
            Bookkeeper's Gambit: v

            // rx packet format: [data, index] | [true]
            Recitation Reflection
            Flock's Disintegration

            // if packet is a single true iota, send the last successfully written index
            // otherwise write this chunk to the specified index
            Gemini Decomposition
            True Reflection
            Equality Distillation
            {
                Bookkeeper's Gambit: v
                Numerical Reflection: 0
                Muninn's Reflection
                Send Iota
            }
            {
                // get focus position
                Gemini Decomposition
                Numerical Reflection: 19
                Cubic Index Distillation
                Numerical Reflection: 19
                Locate Heap
                Additive Distillation
                
                // write data
                Rotation Gambit
                Ritualist's Gambit

                // print written index for debugging, then save it to send later
                Reveal
                Huginn's Gambit
            }
            Augur's Exaltation
            Hermes' Gambit
        }

        // if we received iotas, iterate once for each iota in the queue
        // else send null to tell the computer we just connected
        Postmaster's Reflection
        Gemini Decomposition
        Augur's Purification
        {
            Gemini Decomposition
            Gemini Gambit
            Gemini Decomposition
            Flock's Gambit

            Thoth's Gambit
            Bookkeeper's Gambit: v
        }
        {
            Bookkeeper's Gambit: v
            Numerical Reflection: 0
            Nullary Reflection
            Send Iota
        }
        Augur's Exaltation
        Hermes' Gambit

        Listen
    Consideration: }

    Mind's Reflection
    Compass' Purification
    Mind's Reflection
    Alidade's Purification
    Archer's Distillation

    Undertaker's Gambit
    Undertaker's Gambit

    Numerical Reflection: 100
    Summon Cyclic Wisp

    Entity Purification: Wisp
    Link Others
}
