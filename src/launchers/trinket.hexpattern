#include "../types/hexcasting.hexpattern"

// step processor by one instruction
{
    // get processor position
    Mind's Reflection
    Compass' Purification
    Mind's Reflection
    Alidade's Purification
    Archer's Distillation

    // if sneaking, reset processor state
    Mind's Reflection
    Stadiometer's Purification
    Numerical Reflection: 1.5
    Maximus Distillation
    Vacant Reflection
    {
        Gemini Decomposition
        Vector Reflection -Y
        Additive Distillation
        Nullary Reflection
        Ritualist's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit
    
    // bootstrap processor code
    Gemini Decomposition
    Vector Reflection +Y
    Additive Distillation
    Ritualist's Purification
    Flock's Disintegration
    Hermes' Gambit
    
    // run startup, then eval once, then shutdown
    Jester's Gambit
    Additive Distillation
    Additive Distillation
    Hermes' Gambit
}

// reset registers and PC to initial state
{
    // get processor position
    Mind's Reflection
    Compass' Purification
    Mind's Reflection
    Alidade's Purification
    Archer's Distillation

    // reset processor state
    Vector Reflection -Y
    Additive Distillation
    Nullary Reflection
    Ritualist's Gambit
}

// print targeted data in hexadecimal
{
    {
        Gemini Decomposition
        Augur's Purification
        {
            Blank Reflection
            Numerical Reflection: 2
            Flock's Gambit
            Huginn's Gambit

            {
                Bookkeeper's Gambit: v
                Muninn's Reflection
                Flock's Disintegration
                Jester's Gambit
                
                {
                    <["0", "1", ..., "e", "f"]>
                }
                Flock's Disintegration

                Prospector's Gambit
                Numerical Reflection: 16
                Modulus Distillation
                Selection Distillation

                Jester's Gambit
                Numerical Reflection: 16
                Division Distillation
                Floor Purification
                
                Rotation Gambit II
                Jester's Gambit
                Additive Distillation

                Numerical Reflection: 2
                Flock's Gambit
                Huginn's Gambit
            }

            Numerical Reflection: 8
            Gemini Decomposition
            Gemini Gambit
            Gemini Decomposition
            Flock's Gambit

            Thoth's Gambit
            Bookkeeper's Gambit: v
            Muninn's Reflection
            Numerical Reflection: 1
            Selection Distillation
        }
        {
            Bookkeeper's Gambit: v
            {
                <"0">
            }
            Flock's Disintegration
        }
        Augur's Exaltation
        Hermes' Gambit
    }

    Mind's Reflection
    Compass' Purification
    Mind's Reflection
    Alidade's Purification
    Archer's Distillation
    Ritualist's Purification

    Thoth's Gambit
    
    Speaker's Decomposition
    Huginn's Gambit

    {
        Muninn's Reflection
        Comma Reflection
        Spacing Reflection
        Additive Distillation
        Additive Distillation
        Jester's Gambit
        Additive Distillation
        Huginn's Gambit
    }
    Jester's Gambit
    Thoth's Gambit
    Bookkeeper's Gambit: v

    Muninn's Reflection
    Reveal
}
