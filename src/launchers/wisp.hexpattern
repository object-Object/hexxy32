#include "../macros/registers.hexpattern"
#include "../types/hexcasting.hexpattern"

// run processor indefinitely
{
    // get processor position
    Mind's Reflection
    Compass' Purification
    Mind's Reflection
    Alidade's Purification
    Archer's Distillation
    Gemini Decomposition

    // if sneaking, reset processor state
    Mind's Reflection
    Stadiometer's Purification
    Numerical Reflection: 1.5
    Maximus Distillation
    Vacant Reflection
    {
        Gemini Decomposition
        Vector Reflection -Y
        Additive Distillation
        Nullary Reflection
        Ritualist's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit
    
    // bootstrap processor code
    Gemini Decomposition
    Vector Reflection +Y
    Additive Distillation
    Ritualist's Purification
    Flock's Disintegration
    Hermes' Gambit

    // wrap eval in a quine to run many instructions per tick
    // we're using a quine so we can store eval on the call stack, because otherwise we hit the stack limit
    {
        {
            Bookkeeper's Gambit: - // regular eval code
        }
        Flock's Disintegration
        Hermes' Gambit
        
        // Ibra oniki ra. QUINIO alef a ra.
        {
            Bookkeeper's Gambit: - // self
        }
        Flock's Disintegration
        Numerical Reflection: 6
        Prospector's Gambit
        Surgeon's Exaltation
        
        // run another instruction if we have more than 10^4 ops remaining
        Thanatos' Reflection
        Numerical Reflection: 10
        Numerical Reflection: 4
        Power Distillation
        Maximus Distillation

        // but only if the exit flag isn't set
        Numerical Reflection: 35
        Read Register
        Numerical Reflection: 0
        Equality Distillation
        Conjunction Distillation

        Jester's Gambit
        Vacant Reflection
        Augur's Exaltation
        Hermes' Gambit
    }
    Numerical Reflection: 1
    Rotation Gambit
    Surgeon's Exaltation
    Numerical Reflection: 6
    Prospector's Gambit
    Surgeon's Exaltation

    // wisp loop: run startup, then evals, then shutdown
    Jester's Gambit
    Additive Distillation
    Additive Distillation
    
    // summon wisp
    Jester's Gambit
    Numerical Reflection: 100 // media
    Summon Cyclic Wisp
}
